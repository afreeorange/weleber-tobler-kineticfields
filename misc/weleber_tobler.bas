100 REM SOLID ANGLE OF SPHERICAL POLYGON
110 REM FROM DISTANCES, RD, AND DIRECTIONS, TD (IN DEGREES)
120 REM R. WELEVER AND W. TOBLER 14 NOV 1985
130 REM PROGRAMMED ON AT&T PC 6300 IN GWBASIC/D (DOUBLE PRECISION)
140 REM FOR IBM PC USE BASICA/D
150 DEFDBL A,D,E,P,R,T,X,Y
160 PI=3.141592653589793#
165 N=11:GOTO 180:REM CURRENT DATA HAS 11 PAIRS
170 INPUT "NUMBER OF DATA PAIRS";N
180 DIM RD(N),TD(N),R(N),T(N)
190 FOR I=1 TO N
200     READ RD(I)
210 NEXT I
220 DATA 0,90,90,90,90,90,90,90,90,90,90
230 FOR I=1 TO N
240     READ TD(I)
250 NEXT I
260 DATA 0,18,190,200,210,220,230,240,250,260,270
270 R1=180/PI
280 PRINT:PRINT:PRINT:PRINT N;" OBSERVATIONS"
290 PRINT "POLAR DISTANCES, DIRECTIONS"
300 PRINT "(in degrees)"
310 FOR I=1 TO N
320     IF I<10 THEN PRINT " ";
330     PRINT I;RD(I);TD(I)
340 NEXT I
350 PRINT
360 REM CONVERT TO RADIANS
370 FOR I=1 TO N
380     IF RD(I)<90 THEN 400
390     RD(I)=90#
400     R(I)=RD(I)/R1
410     T(I)=TD(I)/R1
420 NEXT I
430 REM SET RADIANS
440 A=0
450 FOR I=1 TO N
460     K=I+1
470     IF K<=N THEN 490
480     K=1
490     D1=ABS(T(I)-T(K))*.5
500     IF D1=0 THEN 600
510     X=(R(K)-R(I))*.5
520     Y=(R(K)+R(I))*.5
530     T1=1/TAN(D1)
540     D3=COS(X)/COS(Y)*T1
550     D3=2*ATAN(D3)
560     E=D3+2*D1-PI
570     IF T(K)=>T(I) THEN 590
580     E=-E
590     A=A+E
600 NEXT I
610 PRINT "Solid Angle = ";A;" steradians"
620 PAS=A/(4*PI)*100
630 PRINT "  or ";PAS;"  % of Sphere"
640 FOR K=1 TO 5:PRINT:NEXT K:END